* bfbf.bf
* All rights reserved
* author Meng Hsun
* date 02/28/2019
* v1.0

This program was written in Brainfuck
And this is the Brainfuck interpreter
Execute it in a Brainfuck interpreter
or compiled one by Brainfuck compiler

Here's the code and you can modify it
by altering scripts between {xxx} and
{/xxx}
  ++++[->++++++++<]>[->+>++>+++<<<]>>>[->+>+>+>+<<<<]+++++++[->>+>++>+++<<<<]<++.>>>>>---.<<<+.>++.>.<---.>>+++.<<<++.>>---.<<<<<.>+++++++.>>>>+++.>-.<<<++.>>>--.<++.>.<<<.>>>++.<<<.>>>--.<<<<++++++++++.>---.>>>+++++++.<<<<<<.>++++.>>+++.>>--.<+.<<<<.>-----.>>>>>------.++.<.<<<<<.>>>>>+.-.<<<<<.>--.>>.---.<<<.<<+++++++[->+++++++<]>+.+++++.>.<-----.--.+.++++++++.>>>.>>++.>.++.>.-.<<<<<<.>>>>>>-.<<<+.>>>-.<<.>.>++.<<<<<<.>>>++.>>--.<<-.>.>.<--.<<<<.>>>>>>+++.<<++.>>---.<<-.<<<<.>>>>>.<<+.>>>+++.<--.<+.>++.<<.<<<<+.>>>.>>>>[[-]<]>,.
{end_of_script_character}
(----------)
  [
(++++++++++)
  >+>,.
(----------)
{/end_of_script_character}
  ]>+++++++++++[->++++++++++<]>[->+>+>+<<<]>++++.+++.>..>-----.<.>--.[[-]<]+++++[->+++++++++<]>+...[-]++++++++++.[-]<<->>>->>>>>->>+<<<<<<<<<<[<<]>[->+[->>+]->+<[<<]>]>+[->>+]->[
{plus}
(-------------------------------------------)
  [>>>+>+<<<]+[->+]-<<
(+++++++++++++++++++++++++++++++++++++++++++)
{/plus}
  >>>-[+>>>>-[+>>-]+<+<+[-<<+]-<<<<]>[-<<<<
{minus}
(---------------------------------------------)
  [>>>+>+<<<]+[->+]-<<
(+++++++++++++++++++++++++++++++++++++++++++++)
{/minus}
  >>>-[+>>>>-[+>>-]+<-<+[-<<+]-<<<<]>[-<<<<
{left_shift}
(------------------------------------------------------------)
  [>>>+>+<<<]+[->+]-<<
(++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++)
{/left_shift}
  >>>-[+>>>>-[+>>-]<<[>>]+[-<<+]-<<<<]>[-<<<<
{right_shift}
(--------------------------------------------------------------)
  [>>>+>+<<<]+[->+]-<<
(++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++)
{/right_shift}
  >>>-[+>>>>-[+>>-]+->>++[-<<+]-<<<<]>[-<<<<
{getchar}
(--------------------------------------------)
  [>>>+>+<<<]+[->+]-<<
(++++++++++++++++++++++++++++++++++++++++++++)
{/getchar}
  >>>-[+>>>[-]+>-[+>>-]+<[-]
{input_prompt}
(++++++++++.++++++++++++++++++++++++++++++++++++++++++++++++++.----------------------------.)
{/input_prompt}
  ,
{echo_input}
(.)
{/echo_input}
  <+[-<<+]-<<<<]>[-<<<<
{putchar}
(----------------------------------------------)
  [>>>+>+<<<]+[->+]-<<
(++++++++++++++++++++++++++++++++++++++++++++++)
{/putchar}
  >>>-[+>>>[
{output_prompt}
(+++++++++.++++++++++++++++++++++++++++++++++++++++++++++++++++.------------------------------.)
{/output_prompt}
[-]]>-[+>>-]+<
{print_format}
(.)
{/print_format}
  <+[-<<+]-<<<<]>[-<<<<
{while_and_end_while}
ascii of end_while:(---------------------------------------------------------------------------------------------)
  [>>>+>+<<<]+[->+]-<<
ascii of end_while:(+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++)
  >>>-[+>>>>-[+>>-]+<[->>>+>>+<<<<<]>>>[-<<<+>>>]>>[[-]+[-<<+]-<<<<<<<<[<<]+>
ascii of end_while minus while:(++)
  [
ascii of while:(+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++)
  <<
ascii of while:(-------------------------------------------------------------------------------------------)
  ]
ascii of while:(+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++)
  <[->>+]->[-]>>>>>>>-[+>>-]+>>>>]+[-<<+]-<<<<]>[-<<<<
ascii of while:(-------------------------------------------------------------------------------------------)
  [>>>+>+<<<]+[->+]-<<
ascii of while:(+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++)
  >>>-[+<<<[-]<[<<]+>
ascii of while minus end_while:(--)
  [
ascii of end_while:(+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++)
  >>
ascii of end_while:(---------------------------------------------------------------------------------------------)
  ]
ascii of end_while:(+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++)
{/while_and_end_while}
  <<<[->>+]->>>>]>]]]]]]]<<<<[-<[<<]>+>+[->>+]->]<[<<]+>>[->>+]-[<<]>[->+[->>+]->+<[<<]>]>+[->>+]->]++++++++++[->+>+++++++>++++++++++>+++++++++++<<<<]>..>+.>+++++.>.<.>+++++.<-.---.-.<[->>-<<]>>+.